buildscript {
    repositories {
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.1.4'
        classpath "io.realm:realm-gradle-plugin:10.17.0"
        classpath "com.google.android.libraries.mapsplatform.secrets-gradle-plugin:secrets-gradle-plugin:2.0.1"
    }
}
plugins {
    id "com.github.ben-manes.versions" version "0.50.0"
}
allprojects {
    repositories {
        google()
        mavenCentral()
        maven {url "https://jitpack.io"}
    }
}

// exclude betas etc. from com.github.ben-manes.versions
dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}
